AC_SUBST(MISCDEFS)
AC_SUBST(HAVE_LIB_TERMCAP)

dnl In modern autoconf, we should be able to replace this with something
dnl the CHECK_TYPE macro
AC_DEFUN([TYPE_SOCKLEN_T],
[AC_CACHE_CHECK([for socklen_t], ac_cv_type_socklen_t,
[
    AC_TRY_COMPILE(
    [#include <sys/types.h>
    #include <sys/socket.h>],
    [socklen_t len = 42; return 0;],
    ac_cv_type_socklen_t=yes,
    ac_cv_type_socklen_t=no)
])
    if test $ac_cv_type_socklen_t != yes; then
       MISCDEFS="$MISCDEFS -Dsocklen_t=int"
    fi
])

if test -e main/miscellaneous/NOINSTALL ; then

        dnl Not installing miscellaneous so don't test for term.h.
        STATUS=noinstall

else

    dnl Check for socklen_t
    TYPE_SOCKLEN_T

    dnl Check if octave_function(void) fails
    TRY_MKOCTFILE([for octave_function(void)],
                  [#include <octave/ov-fcn.h>
		  class myfun:octave_function {myfun():octave_function(){}};], 
                  [],[MISCDEFS="$MISCDEFS -DOCTAVE_FUNCTION_VOID_FAILS"])

    dnl Check for term.h
    case "$canonical_host_type" in
	*-sgi-*)
	    have_term=no
	    have_termcap=no
	;;
	*)
    	    AC_CHECK_HEADER(term.h, have_term=yes, have_term=no)
	    AC_CHECK_HEADER(termcap.h, have_termcap=yes, have_termcap=no)
	;;
    esac
    if test $have_term = yes; then
       MISCDEFS="$MISCDEFS -DUSE_TERM -DHAVE_TERM_H"
       STATUS=yes
       AC_CHECK_LIB(termcap, tgetnum, HAVE_LIB_TERMCAP=yes)
    elif test $have_termcap = yes; then
       MISCDEFS="$MISCDEFS -DUSE_TERM -DHAVE_TERMCAP_H"
       AC_CHECK_LIB(termcap, tgetnum, HAVE_LIB_TERMCAP=yes)
       STATUS=yes 
    else
       STATUS=no
    fi
    dnl Append the status message to the global status message.  This will
    dnl be displayed at the end of configuration so that the user doesn't
    dnl have to scan the list for critical details.

    STATUS_MSG="$STATUS_MSG
   have term.h or termcap.h: $STATUS"

fi
