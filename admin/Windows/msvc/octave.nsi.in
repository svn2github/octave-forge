; Script generated by the HM NIS Edit Script Wizard.

!define OCTAVE_VERSION "@OCTAVE_VERSION@"
!define VCLIBS_ROOT "@VCLIBS_ROOT@"
!define MSYS_ROOT "@MSYS_ROOT@"
@HAVE_JHANDLES@!define JHANDLES_VERSION "@JHANDLES_VERSION@"
!define SOFTWARE_ROOT "@SOFTWARE_ROOT@"
!define MSVC_CRT "@MSVC_CRT@"
@HAVE_OCTAVE_GRAPHICSMAGICK@!define USE_OCTAVE_GRAPHICSMAGICK
@HAVE_OCTAVE_GRAPHICSMAGICK@!define GMAGICKVER "@GMAGICKVER@"
@HAVE_SSE1@!define USE_SSE1
@HAVE_SSE2@!define USE_SSE2
@HAVE_SSE3@!define USE_SSE3
@HAVE_FLTK_BACKEND@!define USE_FLTK_BACKEND

; Determine which configuration to use
@HAVE_OCTPLOT@!define USE_OCTPLOT
@HAVE_OCTAVE_FORGE@!define USE_OCTAVE_FORGE
!define USE_MSYS
#!define USE_CONSOLE
@HAVE_GUI@!define USE_OCTAVEDE
@HAVE_PLAYREC@!define USE_PLAYREC

!define OCTAVE_BASE "octave-${OCTAVE_VERSION}"

; Location of various components
!define OCTAVE_ROOT "${VCLIBS_ROOT}\local\${OCTAVE_BASE}"
!ifdef USE_CONSOLE
!define CONSOLE_ROOT "${SOFTWARE_ROOT}\Console2"
!endif
!define SCITE_ROOT "${SOFTWARE_ROOT}\wscite"
!define GMSH_ROOT "${SOFTWARE_ROOT}\Gmsh"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "GNU Octave"
!define PRODUCT_VERSION "${OCTAVE_VERSION}"
!define PRODUCT_PUBLISHER ""
!define PRODUCT_WEB_SITE "http://www.octave.org"
!define OCTAVE_FORGE_WEB_SITE "http://octave.sourceforge.net"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\octave-${OCTAVE_VERSION}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"
!define PRODUCT_ROOT_KEY "HKLM"
!define PRODUCT_KEY "Software\Octave"

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "Sections.nsh"
!include "WordFunc.nsh"
!insertmacro WordReplace

!define LA_GEN "Generic (works on all systems)"
!define LA_SSE1 "Intel Pentium with SSE"
!define LA_SSE2 "Intel Pentium with SSE2"
!define LA_SSE3 "Intel Pentium with SSE3"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "octave-hdr.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP "octave.bmp"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_TEXT_BOTTOM "Click Next to continue."
!define MUI_LICENSEPAGE_BUTTON "Next >"
!insertmacro MUI_PAGE_LICENSE "${VCLIBS_ROOT}\license\COPYING.GPL"
; CPU detection page
Page custom AtlasCpu AtlasCpuEnd
; Components page
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE componentsLeave
!insertmacro MUI_PAGE_COMPONENTS
; Graphics backend selection
Page custom Graphics GraphicsEnd
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "GNU Octave"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
;!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README.txt"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "octave-${OCTAVE_VERSION}-setup.exe"
InstallDir "$PROGRAMFILES\Octave"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" "Path"
ShowInstDetails show
ShowUnInstDetails show
XPStyle on

Var IS_WIN2K
Var DEFAULT_GRAPHICS_BACKEND

SectionGroup /e "Core" GRP_CORE

Section "Core" SEC_CORE
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  ; octave executables
  File "${OCTAVE_ROOT}\bin\cruft.dll"
  File "${OCTAVE_ROOT}\bin\octave-${OCTAVE_VERSION}.exe"
  File "${OCTAVE_ROOT}\bin\octave-bug.exe"
  File "${OCTAVE_ROOT}\bin\octave-bug-${OCTAVE_VERSION}.exe"
  File "${OCTAVE_ROOT}\bin\octave.dll"
  File "${OCTAVE_ROOT}\bin\octave.exe"
  File "${OCTAVE_ROOT}\bin\octinterp.dll"
  File "..\cygwin\octave.ico"
  ; octave compiled modules
  SetOutPath "$INSTDIR\libexec"
  File /r /x "packages" "${OCTAVE_ROOT}\libexec\*.*"
  ; octave script modules
  SetOutPath "$INSTDIR\share"
  File /r /x "packages" /x "octave_packages" "${OCTAVE_ROOT}\share\*.*"
  ; support libraries and executables
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\libpcre-0.dll"
  File "${VCLIBS_ROOT}\bin\libreadline-5.dll"
  File "${VCLIBS_ROOT}\bin\libncurses-5.dll"
  File "${VCLIBS_ROOT}\bin\libfftw3-3.dll"
  File "${VCLIBS_ROOT}\bin\libfftw3f-3.dll"
  File "${VCLIBS_ROOT}\bin\libhdf5-0.dll"
  File "${VCLIBS_ROOT}\bin\libglpk-0.dll"
  File "${VCLIBS_ROOT}\bin\fftw-wisdom.exe"
  File "${VCLIBS_ROOT}\bin\fftwf-wisdom.exe"
  File "${VCLIBS_ROOT}\bin\zlib1.dll"
  File "${VCLIBS_ROOT}\bin\libcurl-4.dll"
  File "${VCLIBS_ROOT}\bin\libgnutls-26.dll"
  File "${VCLIBS_ROOT}\bin\libtasn1-3.dll"
  File "${VCLIBS_ROOT}\bin\libgcrypt-11.dll"
  File "${VCLIBS_ROOT}\bin\libgpg-error-0.dll"
  File "${VCLIBS_ROOT}\bin\libftgl-0.dll"
  File "${VCLIBS_ROOT}\bin\libfreetype-6.dll"
!ifdef USE_PLAYREC
  File "${VCLIBS_ROOT}\bin\libportaudio-2.dll"
!endif
!ifdef USE_FLTK_BACKEND
  File "${VCLIBS_ROOT}\bin\fltkdll.dll"
  File "${VCLIBS_ROOT}\bin\libjpeg-62.dll"
  File "${VCLIBS_ROOT}\bin\libpng12-0.dll"
!endif
!ifdef USE_OCTAVE_GRAPHICSMAGICK
  File "${VCLIBS_ROOT}\bin\libjpeg-62.dll"
  File "${VCLIBS_ROOT}\bin\libpng12-0.dll"
  File "${VCLIBS_ROOT}\bin\libGraphicsMagick-1.dll"
  File "${VCLIBS_ROOT}\bin\libGraphicsMagick++-1.dll"
  File "${VCLIBS_ROOT}\bin\libtiff.dll"
  File "${VCLIBS_ROOT}\bin\libjasper-1.dll"
  File "${VCLIBS_ROOT}\bin\libwmflite-0-2-7.dll"
  File "${VCLIBS_ROOT}\bin\libbz2.dll"
  File "${VCLIBS_ROOT}\bin\libxml2-2.dll"
  File "${VCLIBS_ROOT}\bin\libiconv-2.dll"
  SetOutPath "$INSTDIR\lib\GraphicsMagick-${GMAGICKVER}"
  File /r "${VCLIBS_ROOT}\lib\GraphicsMagick-${GMAGICKVER}\*.*"
  SetOutPath "$INSTDIR\share\GraphicsMagick-${GMAGICKVER}"
  File /r "${VCLIBS_ROOT}\share\GraphicsMagick-${GMAGICKVER}\*.*"
!endif
  ; licenses
  SetOutPath "$INSTDIR\license"
  File "${VCLIBS_ROOT}\license\COPYING.GPL"
  File "${VCLIBS_ROOT}\license\COPYING.HDF5"
  File "${VCLIBS_ROOT}\license\COPYING.CURL"
  ; info files
  SetOutPath "$INSTDIR\share\info"
  File "${OCTAVE_ROOT}\share\info\*.*"
  ; NCurses support files
  SetOutPath "$INSTDIR\share\terminfo"
  File /r "${VCLIBS_ROOT}\share\terminfo\*"
  SetOutPath "$INSTDIR\share\tabset"
  File /r "${VCLIBS_ROOT}\share\tabset\*"
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\tic.exe"
  File "${VCLIBS_ROOT}\bin\infocmp.exe"
  ; other files
  SetOutPath "$INSTDIR\doc"
  File "${OCTAVE_ROOT}\doc\README"
  File "${OCTAVE_ROOT}\doc\THANKS"
  File "${OCTAVE_ROOT}\doc\NEWS"
  ; README
  SetOutPath "$INSTDIR"
  File "README.txt"

; Shortcuts
  SetOutPath "$INSTDIR"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Octave.lnk" "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe" "" \
    "$INSTDIR\bin\octave.ico" 0
  CreateShortCut "$DESKTOP\Octave.lnk" "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe" "" \
    "$INSTDIR\bin\octave.ico" 0
  !insertmacro MUI_STARTMENU_WRITE_END

  ; Create __default_graphics__.m
  ClearErrors
  FileOpen $0 "$INSTDIR\share\octave\${OCTAVE_VERSION}\m\startup\__default_graphics__.m" w
  IfErrors error1
  StrCpy $1 $DEFAULT_GRAPHICS_BACKEND
  FileWrite $0 'function ret = __default_graphics__, ret="$1";, endfunction$\n'
  IfErrors error2
  FileClose $0
  Goto done
error2:
  FileClose $0
error1:
  MessageBox MB_OK|MB_ICONSTOP "Error while creating __default_graphics__.m file. Octave may not work correctly."
done:
SectionEnd

Section "Development files" SEC_DEV
  SetOverwrite try
  SetOutPath "$INSTDIR\include"
  File /r "${OCTAVE_ROOT}\include\*.*"
  SetOutPath "$INSTDIR\lib"
  File /r /x *.lib.${OCTAVE_VERSION} /x octave_fixed.lib "${OCTAVE_ROOT}\lib\*.*"
  SetOutPath "$INSTDIR\bin"
  File "${OCTAVE_ROOT}\bin\mkoctfile.exe"
  File "/oname=mkoctfile-${OCTAVE_VERSION}.exe" "${OCTAVE_ROOT}\bin\mkoctfile.exe"
  ;File "${OCTAVE_ROOT}\bin\mkoctfile"
  ;File "${OCTAVE_ROOT}\bin\mkoctfile-${OCTAVE_VERSION}"
  File "${OCTAVE_ROOT}\bin\octave-config.exe"
  File "/oname=octave-config-${OCTAVE_VERSION}" "${OCTAVE_ROOT}\bin\octave-config.exe"
  ;File "${OCTAVE_ROOT}\bin\octave-config"
  ;File "${OCTAVE_ROOT}\bin\octave-config-${OCTAVE_VERSION}"
  File "${VCLIBS_ROOT}\bin\cc-msvc.exe"
  File "${VCLIBS_ROOT}\bin\fc-msvc.exe"
  File "${VCLIBS_ROOT}\bin\f2c.exe"
  File "${VCLIBS_ROOT}\bin\ar-msvc"
  File "${VCLIBS_ROOT}\bin\ranlib-msvc"
  ; Additional dependent library files (required by mkoctfile, although not really used)
  SetOutPath "$INSTDIR\lib"
  File "${VCLIBS_ROOT}\lib\blas.lib"
  File "${VCLIBS_ROOT}\lib\lapack.lib"
  File "${VCLIBS_ROOT}\lib\fftw3.lib"
  File "${VCLIBS_ROOT}\lib\fftw3f.lib"
  File "${VCLIBS_ROOT}\lib\readline.lib"
  File "${VCLIBS_ROOT}\lib\ncurses.lib"
  File "${VCLIBS_ROOT}\lib\hdf5.lib"
  File "${VCLIBS_ROOT}\lib\zlib.lib"
  File "${VCLIBS_ROOT}\lib\f2c.lib"
  ; Additional headers required by some octave headers
  ; HDF5
  SetOutPath "$INSTDIR\include"
  File "${VCLIBS_ROOT}\include\H5*.h"
  File "${VCLIBS_ROOT}\include\hdf5.h"
  File "${VCLIBS_ROOT}\include\stdint.h"
  File "${VCLIBS_ROOT}\include\inttypes.h"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

!ifdef USE_MSYS
!include "octave_msys.nsi"
!endif

SectionGroup /e "Linear Algebra Libraries" GRP_LINALG

Section "Generic" SEC_LA_GEN
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File /oname=libblas.dll "${VCLIBS_ROOT}\bin\libblas_f77.dll"
  File /oname=liblapack.dll "${VCLIBS_ROOT}\bin\liblapack_f77.dll"
  SetOutPath "$INSTDIR\license"
  File "${VCLIBS_ROOT}\license\COPYING.LAPACK"
SectionEnd

!ifdef USE_SSE1
Section /o "Pentium/SSE1" SEC_LA_SSE1
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File /oname=libblas.dll "${VCLIBS_ROOT}\bin\libblas_atl381_PIIISSE1.dll"
  File /oname=liblapack.dll "${VCLIBS_ROOT}\bin\liblapack_atl381_PIIISSE1.dll"
SectionEnd
!endif

!ifdef USE_SSE2
Section /o "Pentium/SSE2" SEC_LA_SSE2
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File /oname=libblas.dll "${VCLIBS_ROOT}\bin\libblas_atl381_P4SSE2.dll"
  File /oname=liblapack.dll "${VCLIBS_ROOT}\bin\liblapack_atl381_P4SSE2.dll"
SectionEnd
!endif

!ifdef USE_SSE3
Section /o "Pentium/SSE3" SEC_LA_SSE3
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File /oname=libblas.dll "${VCLIBS_ROOT}\bin\libblas_atl381_P4E32SSE3.dll"
  File /oname=liblapack.dll "${VCLIBS_ROOT}\bin\liblapack_atl381_P4E32SSE3.dll"
SectionEnd
!endif

SectionGroupEnd

Section /o "C/C++ Runtime Libraries" SEC_VC
  ;Call InstallRuntime
  SetOverwrite try
  StrCmp $IS_WIN2K 1 is_win2k is_winxp
is_winxp:
  SetOutPath "$INSTDIR\bin\Microsoft.VC${MSVC_CRT}.CRT"
  File "${VCLIBS_ROOT}\bin\Microsoft.VC${MSVC_CRT}.CRT\*.*"
  Goto done
is_win2k:
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\Microsoft.VC${MSVC_CRT}.CRT\*.dll"
  Goto done
done:
SectionEnd

SectionGroupEnd

!ifdef USE_OCTAVE_FORGE
SectionGroup "Octave Forge" GRP_FORGE
!include "octave_forge.nsi"
SectionGroupEnd
!endif

SectionGroup /e "Graphics" GRP_GRAPHICS

!ifdef JHANDLES_VERSION
Section "JHandles" SEC_JHANDLES
  SetOverwrite try
  SetOutPath "$INSTDIR\share\octave\packages\jhandles-${JHANDLES_VERSION}"
  File /r "${OCTAVE_ROOT}\share\octave\packages\jhandles-${JHANDLES_VERSION}\*"
  SetOutPath "$INSTDIR\libexec\octave\packages\jhandles-${JHANDLES_VERSION}"
  File /r "${OCTAVE_ROOT}\libexec\octave\packages\jhandles-${JHANDLES_VERSION}\*"
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\jogl.jar"
  File "${VCLIBS_ROOT}\bin\jogl.dll"
  File "${VCLIBS_ROOT}\bin\jogl_awt.dll"
  File "${VCLIBS_ROOT}\bin\jogl_cg.dll"
  File "${VCLIBS_ROOT}\bin\gluegen-rt.jar"
  File "${VCLIBS_ROOT}\bin\gluegen-rt.dll"
  SetOutPath "$INSTDIR\license"
  File "${VCLIBS_ROOT}\license\COPYING.JOGL"
SectionEnd
!endif

Section "Gnuplot" SEC_GNUPLOT
  SetOverwrite try
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\pgnuplot.exe"
  File "${VCLIBS_ROOT}\bin\pgnuplot_win.exe"
  File "${VCLIBS_ROOT}\bin\wgnuplot.exe"
  File "${VCLIBS_ROOT}\bin\wgnuplot.hlp"
  File "${VCLIBS_ROOT}\bin\wgnuplot.mnu"
  File "${VCLIBS_ROOT}\bin\libgd-2.dll"
  File "${VCLIBS_ROOT}\bin\libfontconfig-1.dll"
  File "${VCLIBS_ROOT}\bin\libxml2-2.dll"
  File "${VCLIBS_ROOT}\bin\libfreetype-6.dll"
  File "${VCLIBS_ROOT}\bin\libiconv-2.dll"
  File "${VCLIBS_ROOT}\bin\libintl-8.dll"
  File "${VCLIBS_ROOT}\bin\libjpeg-62.dll"
  File "${VCLIBS_ROOT}\bin\libcairo-2.dll"
  File "${VCLIBS_ROOT}\bin\libglib-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgmodule-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgobject-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgthread-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpango-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpangocairo-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpangowin32-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpng12-0.dll"
  File "${VCLIBS_ROOT}\bin\libpcre-0.dll"
  File "${VCLIBS_ROOT}\bin\zlib1.dll"
  #SetOutPath "$INSTDIR\etc"
  #File /r "${VCLIBS_ROOT}\etc\*.*"
  SetOutPath "$INSTDIR\share\gnuplot"
  File /r "${VCLIBS_ROOT}\share\gnuplot\*.*"
  SetOutPath "$INSTDIR\doc\gnuplot"
  File /r "${VCLIBS_ROOT}\doc\gnuplot*.*"
  SetOutPath "$INSTDIR\etc\pango"
  File /r "${VCLIBS_ROOT}\etc\pango\*.*"
  #SetOutPath "$INSTDIR\lib\pango"
  #File /r "${VCLIBS_ROOT}\lib\pango\*.*"
  SetOutPath "$INSTDIR\license"
  File "${VCLIBS_ROOT}\license\COPYING.LIBGD"
  File "${VCLIBS_ROOT}\license\COPYING.GNUPLOT"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
;  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Help.lnk" "$INSTDIR\bin\wgnuplot.hlp"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

!ifdef USE_OCTPLOT
Section "Octplot" SEC_OPLOT
  SetOverwrite try
  SetOutPath "$INSTDIR\bin"
  File "${VCLIBS_ROOT}\bin\fltkdll.dll"
  File "${VCLIBS_ROOT}\bin\libjpeg-62.dll"
  File "${VCLIBS_ROOT}\bin\libpng12-0.dll"
  File "${VCLIBS_ROOT}\bin\libfreetype-6.dll"
  SetOutPath "$INSTDIR\share\octplot"
  File /r "${OCTAVE_ROOT}\share\octplot\*.*"
  SetOutPath "$INSTDIR\license"
  File "${VCLIBS_ROOT}\license\COPYING.FLTK"
SectionEnd
!endif

SectionGroupEnd

Sectiongroup /e "Documentation" GRP_DOC

Section "HTML" DOC_HTML
  SetOverwrite try
  #SetOutPath "$INSTDIR\doc\HTML\faq"
  #File "${OCTAVE_ROOT}\doc\HTML\faq\*.*"
  SetOutPath "$INSTDIR\doc\HTML\interpreter"
  File "${OCTAVE_ROOT}\doc\HTML\interpreter\*.*"
  SetOutPath "$INSTDIR\doc\HTML\liboctave"
  File "${OCTAVE_ROOT}\doc\HTML\liboctave\*.*"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP\Documentation\HTML"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\HTML\Octave.lnk" "$INSTDIR\doc\HTML\interpreter\index.html"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\HTML\Octave C++ API.lnk" "$INSTDIR\doc\HTML\liboctave\index.html"
  #CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\HTML\FAQ.lnk" "$INSTDIR\doc\HTML\faq\index.html"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "PDF" DOC_PDF
  SetOverwrite try
  SetOutPath "$INSTDIR\doc\PDF"
  File "${OCTAVE_ROOT}\doc\PDF\Octave-FAQ.pdf"
  File "${OCTAVE_ROOT}\doc\PDF\octave.pdf"
  File "${OCTAVE_ROOT}\doc\PDF\octave-a4.pdf"
  File "${OCTAVE_ROOT}\doc\PDF\liboctave.pdf"
  File "${OCTAVE_ROOT}\doc\PDF\refcard-*.pdf"
  
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP\Documentation\PDF"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\PDF\Octave.lnk" "$INSTDIR\doc\PDF\octave.pdf"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\PDF\Quick Refernce Card.lnk" "$INSTDIR\doc\PDF\refcard-a4.pdf"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\PDF\Octave C++ API.lnk" "$INSTDIR\doc\PDF\liboctave.pdf"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\PDF\FAQ.lnk" "$INSTDIR\doc\PDF\Octave-FAQ.pdf"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

SectionGroupEnd

SectionGroup /e "Misc" GRP_MISC

Section "Tools" SEC_TOOLS
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File "${VCLIBS_ROOT}\bin\less.exe"
  File "${VCLIBS_ROOT}\bin\sed.exe"
  File "${VCLIBS_ROOT}\bin\libiconv-2.dll"
  File "${VCLIBS_ROOT}\bin\libintl-8.dll"
  File "${VCLIBS_ROOT}\bin\makeinfo.exe"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "SciTE editor" SEC_SCITE
  SetOutPath "$INSTDIR\tools\wscite"
  SetOverwrite try
  File /r /x License.txt /x matlab.properties "${SCITE_ROOT}\*.*"
  File "matlab.properties.octave"
  StrCpy $0 "$INSTDIR\tools\wscite\matlab.properties.octave"
  StrCpy $1 "$INSTDIR\tools\wscite\matlab.properties"
  Call ReplaceOctDir
  Delete "$INSTDIR\tools\wscite\matlab.properties.octave"
  SetOutPath "$INSTDIR\license"
  File /oname=COPYRIGHT.SCITE "${SCITE_ROOT}\License.txt"
  SetOutPath "$INSTDIR"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\SciTE editor.lnk" "$INSTDIR\tools\wscite\SciTE.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

!ifdef USE_CONSOLE
Section /o "Console" SEC_CONSOLE
  SetOutPath "$INSTDIR\tools\console"
  SetOverwrite try
  File /x console.xml "${CONSOLE_ROOT}\*.*"
  IfFileExists "$WINDIR\system32\msvcr71.dll" no_runtime_71
  File "C:\WINDOWS\system32\msvcr71.dll"
  File "C:\WINDOWS\system32\msvcp71.dll"
no_runtime_71:
  StrCpy $0 "$INSTDIR\tools\console\console_oct.xml"
  StrCpy $1 "$INSTDIR\tools\console\console.xml"
  Call ReplaceOctDir
  Delete "$INSTDIR\tools\console\console_oct.xml"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Octave.lnk" "$INSTDIR\tools\console\Console.exe" \
	"" "$INSTDIR\bin\octave.ico"
  CreateShortCut "$DESKTOP\Octave.lnk" "$INSTDIR\tools\console\Console.exe" "" \
    "$INSTDIR\bin\octave.ico" 0
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd
!endif

!ifdef USE_OCTAVEDE
Section /o "Graphical User-Interface" SEC_OCTAVEDE
  SetOutPath "$INSTDIR\bin"
  SetOverwrite try
  File "${OCTAVE_ROOT}\bin\octavede.exe"
  File "${VCLIBS_ROOT}\bin\libatk-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libatkmm-1.6-1.dll"
  File "${VCLIBS_ROOT}\bin\libcairo-2.dll"
  File "${VCLIBS_ROOT}\bin\libcairomm-1.0-1.dll"
  File "${VCLIBS_ROOT}\bin\libfontconfig-1.dll"
  File "${VCLIBS_ROOT}\bin\libfreetype-6.dll"
  File "${VCLIBS_ROOT}\bin\libgdk-win32-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgdk_pixbuf-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgdkmm-2.4-1.dll"
  File "${VCLIBS_ROOT}\bin\libgdl-1-0.dll"
  File "${VCLIBS_ROOT}\bin\libglade-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libglib-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libglibmm-2.4-1.dll"
  File "${VCLIBS_ROOT}\bin\libgmodule-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgobject-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgtk-win32-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgtkmm-2.4-1.dll"
  File "${VCLIBS_ROOT}\bin\libgtksourceview-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libgthread-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libiconv-2.dll"
  File "${VCLIBS_ROOT}\bin\libintl-8.dll"
  File "${VCLIBS_ROOT}\bin\libjpeg-62.dll"
  File "${VCLIBS_ROOT}\bin\libncurses-5.dll"
  File "${VCLIBS_ROOT}\bin\libpango-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpangocairo-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpangowin32-1.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libpangomm-1.4-1.dll"
  File "${VCLIBS_ROOT}\bin\libpcre-0.dll"
  File "${VCLIBS_ROOT}\bin\libpng12-0.dll"
  File "${VCLIBS_ROOT}\bin\libsigc-2.0-0.dll"
  File "${VCLIBS_ROOT}\bin\libtiff.dll"
  File "${VCLIBS_ROOT}\bin\libvte-9.dll"
  File "${VCLIBS_ROOT}\bin\libxml2-2.dll"
  File "${VCLIBS_ROOT}\bin\zlib1.dll"
  SetOutPath "$INSTDIR\lib\gtk-2.0"
  File /r "${VCLIBS_ROOT}\lib\gtk-2.0\*.*"
  #SetOutPath "$INSTDIR\lib\pango"
  #File /r "${VCLIBS_ROOT}\lib\pango\*.*"
  SetOutPath "$INSTDIR\etc\gtk-2.0"
  File /r "${VCLIBS_ROOT}\etc\gtk-2.0\*.*"
  SetOutPath "$INSTDIR\etc\pango"
  File /r "${VCLIBS_ROOT}\etc\pango\*.*"
  SetOutPath "$INSTDIR\etc\fonts"
  File /r "${VCLIBS_ROOT}\etc\fonts\*.*"
  SetOutPath "$INSTDIR\share\gdl"
  File /r "${VCLIBS_ROOT}\share\gdl\*.*"
  SetOutPath "$INSTDIR\share\gtksourceview-2.0"
  File /r "${VCLIBS_ROOT}\share\gtksourceview-2.0\*.*"
  SetOutPath "$INSTDIR\share\vte"
  File /r "${VCLIBS_ROOT}\share\vte\*.*"
  SetOutPath "$INSTDIR\share\xml"
  File /r "${VCLIBS_ROOT}\share\xml\*.*"

; Shortcuts
  SetOutPath "$INSTDIR"
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Octave UI.lnk" "$INSTDIR\bin\octavede.exe" \
	"" "$INSTDIR\bin\octave.ico"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd
!endif

SectionGroupEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  #CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Octave Home Page.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  WriteIniStr "$SMPROGRAMS\$ICONS_GROUP\Octave Home Page.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\Octave-Forge.url" "InternetShortcut" "URL" "${OCTAVE_FORGE_WEB_SITE}"
  #CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Octave-Forge Home Page.lnk" "$INSTDIR\Octave-Forge.url"
  WriteIniStr "$SMPROGRAMS\$ICONS_GROUP\Octave-Forge Home Page.url" "InternetShortcut" "URL" "${OCTAVE_FORGE_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP\Documentation"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\README.lnk" "$INSTDIR\README.txt"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "Path" "$INSTDIR"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_ROOT_KEY} "${PRODUCT_KEY}\${OCTAVE_VERSION}" "InstallPath" "$INSTDIR"
  WriteRegStr ${PRODUCT_ROOT_KEY} "${PRODUCT_KEY}\${OCTAVE_VERSION}" "Version" "${OCTAVE_VERSION}"

  InitPluginsDir
  File "/oname=$PLUGINSDIR\do_pkg_init.m" "do_pkg_init.m"
  ExecWait '"$INSTDIR\bin\octave.exe" -qf "$PLUGINSDIR\do_pkg_init.m"'
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_CORE} "Octave core files"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_DEV} "Octave development files (include and library files)"
!ifdef USE_MSYS
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_MSYS} "UNIX-like shell environment, required to use the Octave package manager"
!endif
!ifdef USE_OCTAVE_FORGE
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_FORGE} "Additional toolboxes for Octave"
  !include "octave_forge_desc.nsi"
!endif
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_GNUPLOT} "Basic plotting component of Octave. If not selected, Gnuplot binary must in your PATH."
!ifdef USE_OCTPLOT
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_OPLOT} "Alternative graphics/plot engine for Octave"
!endif
!ifdef JHANDLES_VERSION
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_JHANDLES} "Java/OpenGL based 2D/3D graphics backend for Octave with high compatibility with Matlab handle graphics"
!endif
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_TOOLS} "Additional GNU tools required (less, makeinfo, sed...). If not selected, those tools must be available in your PATH."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_VC} "Microsoft C/C++ runtime libraries required by Octave. It is STRONGLY recommended to use the default setting."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_SCITE} "Powerful code editor with syntax highlighting, directly accessible from the octave prompt (http://www.scintilla.org)"
!ifdef USE_CONSOLE
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_CONSOLE} "Advanced console for Windows, with multi-tab and editing capabilities (http://sourceforge.net/projects/console)"
!endif
!ifdef USE_OCTAVEDE
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_OCTAVEDE} "Octave GTK-based graphical user-interface. WARNING: this software is in alpha stage and only provided as a technology preview"
!endif
  
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_CORE} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_GRAPHICS} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_MISC} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_DOC} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${GRP_LINALG} ""
  
  !insertmacro MUI_DESCRIPTION_TEXT ${DOC_HTML} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${DOC_PDF} ""
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_LA_GEN} ""
!ifdef USE_SSE3
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_LA_SSE3} ""
!endif
!ifdef USE_SSE2
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_LA_SSE2} ""
!endif
!ifdef USE_SSE1
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC_LA_SSE1} ""
!endif
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
  Call un.DetectAdmin
  Pop $0
  StrCmp $0 1 0 endadmin
  SetShellVarContext all
endadmin:
FunctionEnd

Function .onInit
  Call CheckPrevVersion
  InitPluginsDir 
  File "/oname=$PLUGINSDIR\check_cpu_flag.exe" "check_cpu_flag.exe"
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "atlascpu.ini"
  !insertmacro MUI_INSTALLOPTIONS_EXTRACT "graphics.ini"
  StrCpy $0 "${LA_GEN}"
!ifdef USE_SSE1
  StrCpy $0 "$0|${LA_SSE1}"
!endif
!ifdef USE_SSE2
  StrCpy $0 "$0|${LA_SSE2}"
!endif
!ifdef USE_SSE3
  StrCpy $0 "$0|${LA_SSE3}"
!endif
  !insertmacro MUI_INSTALLOPTIONS_WRITE "atlascpu.ini" "Field 3" "ListItems" "$0"
  !insertmacro SetSectionFlag ${SEC_CORE} ${SF_RO}
  ;!insertmacro SetSectionFlag ${SEC_VC} ${SF_RO}
  !insertmacro SetSectionFlag ${SEC_LA_GEN} ${SF_RO}
!ifdef USE_SSE1
  !insertmacro SetSectionFlag ${SEC_LA_SSE1} ${SF_RO}
!endif
!ifdef USE_SSE2
  !insertmacro SetSectionFlag ${SEC_LA_SSE2} ${SF_RO}
!endif
!ifdef USE_SSE3
  !insertmacro SetSectionFlag ${SEC_LA_SSE3} ${SF_RO}
!endif
  Call DetectWinVer
  Call CheckMSVCRT
  Pop $0
  StrCmp $0 1 noruntime
  !insertmacro SetSectionFlag ${SEC_VC} ${SF_SELECTED}
noruntime:
  Call DetectJVM
  Pop $0
  StrCmp "" "$0" nojvm jvm
jvm:
!ifdef JHANDLES_VERSION
  !insertmacro SetSectionFlag ${SEC_java} ${SF_SELECTED}
  !insertmacro SetSectionFlag ${SEC_JHANDLES} ${SF_SELECTED}
!endif
  Goto endjvm
nojvm:
!ifdef JHANDLES_VERSION
  !insertmacro ClearSectionFlag ${SEC_java} ${SF_SELECTED}
  !insertmacro ClearSectionFlag ${SEC_JHANDLES} ${SF_SELECTED}
!endif
endjvm:
!ifdef USE_MSYS
  Call DetectMSYS
  Pop $0
  StrCmp $0 1 msys nomsys
msys:
  !insertmacro ClearSectionFlag ${SEC_MSYS} ${SF_SELECTED}
  Goto endmsys
nomsys:
  !insertmacro SetSectionFlag ${SEC_MSYS} ${SF_SELECTED}
endmsys:
!endif
  Call DetectAdmin
  Pop $0
  StrCmp $0 1 0 endadmin
  SetShellVarContext all
endadmin:
FunctionEnd

!macro CheckCPUFlag flag
  Push ${flag}
  Call CheckCPUFlag
!macroend

Function CheckCPUFlag
  Exch $0
  ExecWait "$PLUGINSDIR\check_cpu_flag.exe $0" $0
  IntOp $0 $0 !
  Exch $0
FunctionEnd

!macro CheckCPUFeature flag
  Push ${flag}
  Call CheckCPUFeature
!macroend

Function CheckCPUFeature
  Exch $1
  System::Call "kernel32::IsProcessorFeaturePresent(i) &i1 (r1) .r1"
  Exch $1
FunctionEnd

Function AtlasCpu
  !insertmacro MUI_HEADER_TEXT "CPU selection" "Choose the CPU type corresponding to your system."
  ReadRegStr $0 HKLM HARDWARE\DESCRIPTION\System\CentralProcessor\0 "ProcessorNameString"
  #!insertmacro CheckCPUFeature 13
  !insertmacro CheckCPUFlag "sse3"
  Pop $1
  StrCmp $1 0 nosse3 0
  StrCpy $1 " (SSE3 detected)"
!ifdef USE_SSE3
  !insertmacro MUI_INSTALLOPTIONS_WRITE "atlascpu.ini" "Field 3" "State" "${LA_SSE3}"
!endif
  Goto atlasend
nosse3:
  StrCpy $1 " (SSE3 not detected)"
atlasend:
  !insertmacro MUI_INSTALLOPTIONS_WRITE "atlascpu.ini" "Field 2" "Text" "$0$1"
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "atlascpu.ini"
FunctionEnd

Function AtlasCpuEnd
validate:
  !insertmacro ClearSectionFlag ${SEC_LA_GEN} ${SF_SELECTED}
!ifdef USE_SSE1
  !insertmacro ClearSectionFlag ${SEC_LA_SSE1} ${SF_SELECTED}
!endif
!ifdef USE_SSE2
  !insertmacro ClearSectionFlag ${SEC_LA_SSE2} ${SF_SELECTED}
!endif
!ifdef USE_SSE3
  !insertmacro ClearSectionFlag ${SEC_LA_SSE3} ${SF_SELECTED}
!endif
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "atlascpu.ini" "Field 3" "State"
  StrCmp $0 "${LA_GEN}" generic0
!ifdef USE_SSE3
  StrCmp $0 "${LA_SSE3}" p4sse30
!endif
!ifdef USE_SSE2
  StrCmp $0 "${LA_SSE2}" p4sse20
!endif
!ifdef USE_SSE1
  StrCmp $0 "${LA_SSE1}" p4sse10
!endif
  Abort "Internal error: unexpected CPU type"
generic0:
  !insertmacro SetSectionFlag ${SEC_LA_GEN} ${SF_SELECTED}
  Goto atlasend
!ifdef USE_SSE3
p4sse30:
  !insertmacro SetSectionFlag ${SEC_LA_SSE3} ${SF_SELECTED}
  Goto atlasend
!endif
!ifdef USE_SSE2
p4sse20:
  !insertmacro SetSectionFlag ${SEC_LA_SSE2} ${SF_SELECTED}
  Goto atlasend
!endif
!ifdef USE_SSE1
p4sse10:
  !insertmacro SetSectionFlag ${SEC_LA_SSE1} ${SF_SELECTED}
  Goto atlasend
!endif
atlasend:
FunctionEnd

Function Graphics
  Push $0
  !insertmacro MUI_HEADER_TEXT "Graphics backend selection" "Choose the graphics backend you want to use by default."
!ifdef USE_OCTPLOT
  !insertmacro SectionFlagIsSet ${SEC_OPLOT} ${SF_SELECTED} octplot no_octplot
octplot:
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 5" "Flags" ""
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 6" "Flags" "NOTABSTOP"
  Goto end_octplot
no_octplot:
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 5" "Flags" "DISABLED"
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 6" "Flags" "NOTABSTOP|DISABLED"
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "graphics.ini" "Field 5" "State"
  StrCmp $0 0 end_octplot
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 1" "State" 1
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 5" "State" 0
end_octplot:
!else
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Settings" "NumFields" "4"
!endif
!ifdef JHANDLES_VERSION
  !insertmacro SectionFlagIsSet ${SEC_JHANDLES} ${SF_SELECTED} jhandles no_jhandles
jhandles:
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 3" "Flags" ""
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 4" "Flags" "NOTABSTOP"
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 1" "State" 0
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 3" "State" 1
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 5" "State" 0
  Goto end_jhandles
no_jhandles:
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 3" "Flags" "DISABLED"
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 4" "Flags" "NOTABSTOP|DISABLED"
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "graphics.ini" "Field 3" "State"
  StrCmp $0 0 end_jhandles
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 1" "State" 1
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Field 3" "State" 0
end_jhandles:
!else
  !insertmacro MUI_INSTALLOPTIONS_WRITE "graphics.ini" "Settings" "NumFields" "2"
!endif
  !insertmacro MUI_INSTALLOPTIONS_DISPLAY "graphics.ini"
  Pop $0
FunctionEnd

Function GraphicsEnd
  Push $0
check_gnuplot:
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "graphics.ini" "Field 1" "State"
  StrCmp $0 0 check_jhandles
  StrCpy $DEFAULT_GRAPHICS_BACKEND "gnuplot"
  Goto done
check_jhandles:
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "graphics.ini" "Field 3" "State"
  StrCmp $0 0 check_octplot
  StrCpy $DEFAULT_GRAPHICS_BACKEND "jhandles"
  Goto done
check_octplot:
  !insertmacro MUI_INSTALLOPTIONS_READ $0 "graphics.ini" "Field 5" "State"
  StrCmp $0 0 default
  StrCpy $DEFAULT_GRAPHICS_BACKEND "octplot"
  Goto done
default:
  StrCpy $DEFAULT_GRAPHICS_BACKEND "gnuplot"
done:
  Pop $0
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  RMDir /r "$INSTDIR"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave Home Page.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave-Forge Home Page.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave Home Page.url"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave-Forge Home Page.url"
;  Delete "$SMPROGRAMS\$ICONS_GROUP\Help.lnk"
  Delete "$DESKTOP\Octave.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Octave UI.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\SciTE editor.lnk"
  RMDir /r "$SMPROGRAMS\$ICONS_GROUP\Documentation"

  RMDir "$SMPROGRAMS\$ICONS_GROUP"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey ${PRODUCT_ROOT_KEY} "${PRODUCT_KEY}\${OCTAVE_VERSION}"
  SetAutoClose true
SectionEnd

Function CheckMSVCRT
  Push $0
  Push $1
  Push $2
  Push $3
  Push $4
  StrCpy $0 0
  FindFirst $1 $2 "$WINDIR\WinSxS\*"
  # Under Win2K, force runtime installation
  StrCmp $IS_WIN2K 1 done 0
  loop:
    StrCmp $2 "" done
    FindFirst $3 $4 "$WINDIR\WinSxS\$2\msvcr${MSVC_CRT}.dll"
    FindClose $3
    StrCmp $4 "" 0 found
    FindNext $1 $2
    Goto loop
  found:
    StrCpy $0 1
  done:
    FindClose $0
  Pop $4
  Pop $3
  Pop $2
  Pop $1
;  Pop $0
  Exch $0
FunctionEnd

Function CheckPrevVersion
  Push $0
  Push $1
  Push $2
  IfFileExists "$INSTDIR\bin\octave-${OCTAVE_VERSION}.exe" 0 otherver
  MessageBox MB_OK|MB_ICONSTOP "Another Octave installation (with the same version) has been detected.$\r$\nPlease uninstall it first."
  Abort
otherver:
  StrCpy $0 0
  StrCpy $2 ""
loop:
  EnumRegKey $1 ${PRODUCT_ROOT_KEY} "${PRODUCT_KEY}" $0
  StrCmp $1 "" loopend
  IntOp $0 $0 + 1
  StrCmp $2 "" 0 +2
  StrCpy $2 "$1"
  StrCpy $2 "$2, $1"
  Goto loop
loopend:
  ReadRegStr $1 ${PRODUCT_ROOT_KEY} "${PRODUCT_KEY}" "Version"
  IfErrors finalcheck
  StrCmp $2 "" 0 +2
  StrCpy $2 "$1"
  StrCpy $2 "$2, $1"
finalcheck:
  StrCmp $2 "" done
  MessageBox MB_YESNO|MB_ICONEXCLAMATION "Another Octave installation (version $2) has been detected.$\r$\nIt is recommended to uninstall it if you intend to use the same installation directory.$\r$\nDo you want to proceed with the installation anyway?" IDYES done IDNO 0
  Abort
done:
  ClearErrors
  Pop $2
  Pop $1
  Pop $0
FunctionEnd

Function DetectAdmin
  Push $0
  Push $1
  ClearErrors
  UserInfo::GetName
  IfErrors win9x
  Pop $0
  UserInfo::GetAccountType
  Pop $1
  StrCmp $1 "Admin" win9x 0
  StrCpy $0 0
  Goto done
win9x:
  StrCpy $0 1
done:
  Pop $1
  Exch $0
FunctionEnd

Function un.DetectAdmin
  Push $0
  Push $1
  ClearErrors
  UserInfo::GetName
  IfErrors win9x
  Pop $0
  UserInfo::GetAccountType
  Pop $1
  StrCmp $1 "Admin" win9x 0
  StrCpy $0 0
  Goto done
win9x:
  StrCpy $0 1
done:
  Pop $1
  Exch $0
FunctionEnd

Function ReplaceOctDir
  Push $0
  Push $1
  Push $2
  Push $3
  ClearErrors
  FileOpen $2 $0 r
  IfErrors done
  FileOpen $3 $1 w
  IfErrors close
doit:
  FileRead $2 $0
  StrCmp $0 "" close1 0
  ${WordReplace} $0 "@OCTAVE_DIR@" "$INSTDIR" "+" $1
  FileWrite $3 $1
  Goto doit
close1:
  FileClose $3
close:
  FileClose $2
done:
  Pop $3
  Pop $2
  Pop $1
  Pop $0
FunctionEnd

Function DetectJVM
  Push $0
  ReadRegStr $0 HKLM "SOFTWARE\JavaSoft\Java Runtime Environment" CurrentVersion
  Exch $0
FunctionEnd

!ifdef USE_MSYS
Function DetectMSYS
  Push $0
  StrCpy $0 1
  IfFileExists "$WINDIR\MSYS.INI" done
  StrCpy $0 0
done:
  Exch $0
FunctionEnd
!endif

Function DetectWinVer
  Push $0
  Push $1
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
  IfErrors is_error is_winnt
is_winnt:
  StrCpy $1 $0 1
  StrCmp $1 6 is_winxp
  StrCmp $1 5 0 is_error
  StrCmp $0 "5.0" is_win2k
  StrCmp $0 "5.1" is_winxp
  StrCmp $0 "5.2" is_winxp64
  Goto is_error
is_win2k:
  StrCpy $IS_WIN2K 1
  Goto done
is_winxp64:
is_winxp:
  StrCpy $IS_WIN2K 0
  Goto done
is_error:
  StrCpy $1 $0
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" ProductName
  IfErrors 0 +4
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion" Version
  IfErrors 0 +2
  StrCpy $0 "Unknown"
  MessageBox MB_ICONSTOP|MB_OK "This version of Octave cannot be installed on this system.$\r$\nSupported systems are Windows 2000 and Windows XP.$\r$\n$\r$\nCurrent system: $0 (version: $1)"
  Abort
done:
  Pop $1
  Pop $0
FunctionEnd

Var DEP_FLAG

!macro CheckDependency pack1 pack2
  Push ${pack1}
  Push ${pack2}
  Call CheckDependency
!macroend

Function CheckDependency
  Exch $1
  Exch
  Exch $0
  !insertmacro SectionFlagIsSet $0 ${SF_SELECTED} 0 done
  !insertmacro SectionFlagIsSet $1 ${SF_SELECTED} done ""
  StrCpy $DEP_FLAG 1
  !insertmacro SetSectionFlag $1 ${SF_SELECTED}
done:
  Pop $0
  Pop $1
FunctionEnd

Function componentsLeave
  StrCpy $DEP_FLAG 0
!ifdef USE_OCTAVE_FORGE
!ifdef JHANDLES_VERSION
  !insertmacro CheckDependency ${SEC_JHANDLES} ${SEC_java}
!endif
  !include "octave_forge_deps.nsi"
!endif
  StrCmp $DEP_FLAG 0 +2
  MessageBox MB_OK|MB_ICONINFORMATION "Some components were automatically selected to satisfy dependencies"
!ifdef JHANDLES_VERSION
  !insertmacro SectionFlagIsSet ${SEC_JHANDLES} ${SF_SELECTED} check_jvm ""
!endif
!ifdef USE_OCTAVE_FORGE
  !insertmacro SectionFlagIsSet ${SEC_java} ${SF_SELECTED} check_jvm done
!endif
check_jvm:
  Call DetectJVM
  Pop $0
  StrCmp "" "$0" 0 done
  MessageBox MB_ICONEXCLAMATION|MB_YESNO "The Java and/or JHandles components have been selected. These components require$\r$\nthe Java Runtime Environment from Sun (http://java.sun.com) installed on your system,$\r$\nbut it seems Java is not available on this system. Installing those components without$\r$\nJava available might prevent Octave from working correctly.$\r$\n$\r$\nProceed with installation anway?" IDYES done
  Abort
done:
FunctionEnd
